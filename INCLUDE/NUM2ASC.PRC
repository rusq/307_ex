;---------------------------------------------------
; NumToASCII Преобразует беззнаковое двоичное значение в ASCII
;-------------------------------
; Вход: ax = 16-битовое преобразуемое значение
;       bx = основание результата (2-BIN,10-DEC,16-HEX)
;	cx = минимальн. кол-во выводимых цифр
;	di = адрес строки с результатом
; Замечания: см. в книге
; Выход: отсутствует
; Регистры: ax,cx
;------------------------------------------------------
NumToASCII	proc	near
	push	dx
	push	di
	push	si

	xor	si,si
	jcxz	@@20
@@10:
	xor	dx,dx
	div	bx
	call	HexDigit
	push	dx
	inc	si
	loop	@@10
@@20:
	inc	cx
	or	ax,ax
	jnz	@@10
	mov	cx,si
	jcxz	@@40
	cld
@@30:
	pop	ax
	stosb
	loop	@@30
@@40:
	mov	byte ptr di,0
	pop	si
	pop	di
	pop	dx

	ret
NumToASCII	endp
;------------------------------------------------------
; HexDigit Преобразует 4-битовое значение в ASCII цифру
;------------------------------------------------------
; Вход: dl = значение в диапазоне 0..15
; Выход: dl = шестнадцатеричный эквивалент ASCII цифры
; Регистры: dl
;------------------------------------------------------
HexDigit proc
	cmp	dl,10
	jb	@@10
	add	dl,'A'-10
	ret
@@10:
	or	dl,'0'
	ret
HexDigit endp
